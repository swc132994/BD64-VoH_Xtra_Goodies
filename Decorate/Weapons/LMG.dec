Actor LMGSpawner : CustomInventory
{
Scale 0.82
Tag "Light Machinegun"
Inventory.PickupMessage "You got the Light Machinegun!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		GLRD B 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		GLRD B 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		GLRD B -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64LMG",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64LMGTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64LMG")
		TNT1 A 0 A_SpawnItemEx("64LMGTACTICAL")
		TNT1 A 1050
		Loop
	}
}

//Upgraded Rifle
ACTOR GrenTrail
{
Scale 0.075
RenderStyle Translucent
Alpha 0.5
+CLIENTSIDEONLY
+NOBLOCKMAP
+DONTSPLASH
+NOCLIP
+NOINTERACTION
+THRUACTORS
	States
	{
	Spawn:
		TNT1 A 2
		PDBA ABCDEFGHIJKL 1 A_FadeOut(0.05)
		Stop
	}
}

actor RocketUpgraded : Rocket
{
Scale 0.22
Damage (random(125,150))
Speed 35
Gravity 0.2
Radius 4
Height 4
Decal "BigScorch"
+EXTREMEDEATH
-DEHEXPLOSION
-ROCKETTRAIL
-NOGRAVITY
	States
	{
	Spawn:
		AGRN A 1 A_CustomMissile("GrenTrail",FRandom(-1,1),FRandom(-1,1))
		Loop
	Death:
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("64ExplosionFireBase",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLO3",CHAN_VOICE)
		TNT1 A 0 A_PlaySound("EXPLOSION",CHAN_WEAPON)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 2
		TNT1 A 0 A_Explode
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		MISL BCDEF 1 Bright
		stop
	}
}

Actor FiredLMG : Inventory
{
inventory.maxamount 4
}

Actor SwitchingGrenade : Inventory
{
inventory.maxamount 1
}

Actor GLReloading : Inventory
{
inventory.maxamount 1
}

ACTOR LMGAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 41
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 41
Inventory.Icon "GLRDA0"
}

ACTOR Argrenade : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 1
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 1
Inventory.Icon "GLRDB0"
}

Actor 64LMG : DoomWeapon
{
Scale 0.85
Weapon.SlotNumber 4
Weapon.SelectionOrder 5
Weapon.AmmoUse1 1
Weapon.AmmoGive1 40
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 6
Weapon.AmmoType2 "RocketAmmo"
Inventory.PickupMessage "You got the Light Machinegun!"
Tag "Light Machinegun"
Obituary "%o was picked off by %k's Light Machinegun."
VisibleToPlayerClass "64DoomPlayer"
Inventory.forbiddento "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+WEAPON.AMMO_CHECKBOTH
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		GLRD B -1
		Stop
	Ready:
		GLRG A 1 A_WeaponReady
		Loop
	Select:
		GLRG A 1 A_Raise
		Loop
	Deselect:
		GLRG A 1 A_Lower
		Loop
	Fire:
		GLRF A 1 Bright
		TNT1 A 0 A_JumpIfInventory("FiredLMG",4,"Fire4")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",3,"Fire3")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",2,"Fire2")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",1,"Fire1")
	Fire0:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(0.0,0.0,-1,9,"RifleBulletPuff",1)
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire1:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(0.6,0.6,-1,9,"RifleBulletPuff",1)
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 //A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire2:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(2.0,2.0,-1,9,"RifleBulletPuff",1)
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 //A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire3:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(1.5,1.5,-1,9,"RifleBulletPuff",1)
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 //A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire4:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(1.0,1.0,-1,9,"RifleBulletPuff",1)
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 //A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	FinishFire:
		TNT1 A 0 A_TakeInventory("FiredLMG",4)
		Goto Ready
	AltFire:
		TNT1 A 0 A_PlaySound("assaultrifle/grenade",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 Radius_Quake(4,3,0,1,0)
		TNT1 A 0 A_Light1
		AGFR A 1 Bright A_FireCustomMissile("RocketUpgraded",0,0)
		TNT1 A 0 A_TakeInventory("RocketAmmo",1)
		AGFR B 1 Bright A_Light2
		AGFR C 1 A_Light0
		AGFR D 1
		GLRG A 20
		Goto Ready
	}
}

Actor 64LMGTactical : Brutal64Weapon
{
Scale 0.85
Weapon.SlotNumber 4
Weapon.SelectionOrder 5
Weapon.AmmoType1 "64HighCaliberBox"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 40
Weapon.AmmoType2 "LMGAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Tag "Light Machinegun"
Obituary "%o was picked off by %k's Light Machinegun."
Inventory.PickupMessage "You got the Light Machinegun!"
Inventory.PickupSound "WPICK"
Inventory.forbiddento "64DoomPlayer"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
Weapon.SisterWeapon "64LMGGrenadeModeTactical"
	States
	{
	Spawn:
		GLRD B -1
		Stop
	v1Ready:
	Ready:
		GLRG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction",1,"GoToGrenadeMode")
		Loop
	NoAmmo:
		GLRG A 1
		GLRG A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	Select:
		TNT1 A 0 A_PlaySound("DSARPRM",CHAN_AUTO)
		TNT1 A 0 A_Print("Weapon Special Available",1)
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",1)
		TNT1 A 0 A_JumpIfInventory("SwitchingGrenade",1,"SelectBulletMode")
		GLRS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	SelectBulletMode:
		GLRG A 1 Offset(0,33)
		GLRG A 1 Offset(0,34)
		GLRG A 2 Offset(0,35) A_TakeInventory("SwitchingGrenade",1)
		GLRG A 1 Offset(0,34)
		GLRG A 1 Offset(0,33)
		Goto Ready
	Fidget:
		GLRS EFGH 1 A_WeaponReady
		GLRS I 1 A_PlaySound("DSARPRM",CHAN_AUTO)
		GLRS JKKK 2 A_WeaponReady
		GLRS IHGFE 1 A_WeaponReady
		Goto Ready
	ADSFidget:
		GL2G BCDE 1 A_WeaponReady
		GL2G F 1 A_PlaySound("DSARPRM",CHAN_AUTO)
		GL2G GHHH 2 A_WeaponReady
		GL2G FEDCB 1 A_WeaponReady
		Goto Ready2
	Deselect:
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_JumpIfInventory("SwitchingGrenade",1,"DeselectToGrenadeMode")
		TNT1 A 0 A_Takeinventory("SwitchingGrenade",0)
		GLRS LMNO 1
		TNT1 A 0 A_Lower
		Wait
	DeselectToGrenadeMode:
		TNT1 A 0 A_Lower
		Wait
	GoToGrenadeMode:
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",1)
		TNT1 A 0 A_GiveInventory("SwitchingGrenade",1)
		TNT1 A 0 A_Print("Grenade Mode")
		TNT1 A 0 A_SelectWeapon("64LMGGrenadeModeTactical")
		TNT1 A 0 A_WeaponReady
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		Goto AmmoCheck
	ResumeFire:
		TNT1 A 0 A_SetPitch(pitch-0.2)
		GLRF A 1 Bright
		TNT1 A 0 A_JumpIfInventory("FiredLMG",4,"Fire4")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",3,"Fire3")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",2,"Fire2")
		TNT1 A 0 A_JumpIfInventory("FiredLMG",1,"Fire1")
	Fire0:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(0.0,0.0,-1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1 A_SetPitch(pitch+0.1)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire1:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(0.6,0.6,-1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1 A_SetPitch(pitch+0.1)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire2:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(2.0,2.0,-1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1 A_SetPitch(pitch+0.25)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire3:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(1.5,1.5,-1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1 A_SetPitch(pitch+0.1)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	Fire4:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-2,2))
		TNT1 A 0 A_FireBullets(1.0,1.0,-1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON,0.9)
		GLRF BC 1 A_SetPitch(pitch+0.1)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-16)
		TNT1 A 0 A_GiveInventory("FiredLMG",1)
		GLRG A 1 A_SetAngle(angle + random(-1,1))
		TNT1 A 0 A_Refire
		Goto FinishFire
	FinishFire:
		TNT1 A 0 A_TakeInventory("FiredLMG",4)
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ADSReload")
		GLRG A 1 A_WeaponReady
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",41,"Fidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		LMGR ABCDEF 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("LMGOUT",CHAN_AUTO)
		LMGR GGHIJK 1
		LMGR L 1 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-16)
		LMGR MNOPPPPPP 1 A_JumpIfInventory("Kicking",1,"DoKick")
		LMGR QRS 1
		TNT1 A 0 A_PlaySound("LMGIN",CHAN_AUTO)
		LMGR TUVXYZ 2
		LM1R ABCDEF 1
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",1,"InsertBullets2")//30+1 effect
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",40,"Ready")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("LMGAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",41,"Ready")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready
		TNT1 A 0 A_Giveinventory("LMGAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
	ADSReload:
		GL2G A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",41,"ADSFidget")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		GL2R ABCDEF 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_PlaySound("LMGOUT",CHAN_AUTO)
		GL2R G 6
		GL2R G 2 Offset(0,34) A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-14)
		GL2R G 15 A_JumpIfInventory("Kicking",1,"DoKick")
		GL2R G 2 Offset(0,33) A_PlaySound("LMGIN",CHAN_AUTO)
		GL2R EFDCBA 2
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",1,"InsertBulletsADS2")//30+1 effect
	InsertBulletsADS:
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",40,"Ready2")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_Giveinventory("LMGAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1)
		Goto InsertBulletsADS
	InsertBulletsADS2:
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",41,"Ready2")
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto Ready2
		TNT1 A 0 A_Giveinventory("LMGAmmo",1)
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		Goto InsertBulletsADS2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(2.0)
		GLFZ ABC 1
		Goto Ready2
	NoAim:
		GLFZ CBA 1
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Ready
	Ready2:
		GL2G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	FireZoomed:
		Goto AmmoCheck
	ResumeFireZoomed:
		TNT1 A 0 A_FireCustomMissile("Tracer",0,0,0,-6,0,0)
		TNT1 A 0 A_FireBullets(0.0,0.0,1,9,"RifleBulletPuff",0)
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,4,-10)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("LMG",CHAN_WEAPON)
		GL2F A 2 Bright A_SetPitch(-1.0 + pitch)
		GL2F BC 1 A_SetPitch(0.5 + pitch)
		TNT1 A 0 A_Refire
		GLFZ C 1 A_WeaponReady
		Goto Ready2
	MineThrowFlash:
		GLRS LMNO 1
		TNT1 A 18
		GLRS ABCD 1
		Stop
	KickingFlash:
		GLRG BCDE 1
		GLRG F 8
		GLRG EDCB 1
		Stop
	AirKickingFlash:
		GLRG BCDE 1
		GLRG F 10
		GLRG EDCB 1
		Stop
	SlideKickingStart:
		GLRG BCDE 1
		GLRG F 18
	SlideKickingEnd:
		GLRG F 6
		GLRG EDCB 1
		Stop
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("LMGAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("LMGAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeFireZoomed")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("64HighCaliberBox",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("64HighCaliberBox",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeFireZoomed")
		Goto ResumeFire
	}
}

Actor 64LMGGrenadeModeTactical : Brutal64Weapon
{
Weapon.AmmoType1 "RocketAmmo"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 6
Weapon.AmmoType2 "ARGrenade"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.forbiddento "64DoomPlayer"
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_P","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.AMMO_CHECKBOTH
+WEAPON.NOALERT
+WEAPON.NOAUTOFIRE
+POWERED_UP
+INVENTORY.UNDROPPABLE
+Inventory.untossable
Weapon.SisterWeapon "64LMGTactical"
	States
	{
	Spawn:
		GLRD B -1
		Stop
	v1Ready:
	Ready:
		GLRG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction",1,"GoToBulletMode")
		Loop
	NoAmmo:
		GLRG A 1
		GLRG A 10 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	Select:
		TNT1 A 0 A_PlaySound("DSARPRM",CHAN_AUTO)
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",1)
		TNT1 A 0 A_JumpIfInventory("SwitchingGrenade",1,"SelectGrenadeMode")
		GLRS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	SelectGrenadeMode:
		GLRG A 1 Offset(0,33)
		GLRG A 1 Offset(0,34)
		GLRG A 2 Offset(0,35) A_TakeInventory("SwitchingGrenade",1)
		GLRG A 1 Offset(0,34)
		GLRG A 1 Offset(0,33)
		Goto Ready
	Fidget:
		AGLR ABCD 1 A_WeaponReady
		AGLR E 1 A_PlaySound("assaultrifle/grenadeopen",CHAN_WEAPON)
		AGLR G 25
		AGLR E 1 A_PlaySound("assaultrifle/grenadeclose",CHAN_WEAPON)
		AGLR DCBA 1 A_WeaponReady
		Goto Ready
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_JumpIfInventory("SwitchingGrenade",1,"DeselectToBulletMode")
		GLRS LMNO 1
		TNT1 A 0 A_Takeinventory("SwitchingGrenade",0)
		TNT1 A 0 A_Lower
		Wait
	DeselectToBulletMode:
		TNT1 A 0 A_Lower
		Wait
	GoToBulletMode:
		TNT1 A 0 A_TakeInventory("WeaponSpecialAction",1)
		TNT1 A 0 A_Print("Regular Mode")
		TNT1 A 0 A_GiveInventory("SwitchingGrenade",1)
		TNT1 A 0 A_SelectWeapon("64LMGTactical")
		Goto Ready
	Fire:
		Goto AmmoCheck
	ResumeFire:
		TNT1 A 0 A_PlaySound("assaultrifle/grenade",CHAN_WEAPON)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash
		TNT1 A 0 Radius_Quake(4,3,0,1,0)
		TNT1 A 0 A_Light1
		AGFR A 1 Bright A_FireCustomMissile("RocketUpgraded",0,0)
		AGFR B 1 Bright A_Light2
		AGFR C 1 A_Light0
		AGFR D 1
		GLRG A 11
		TNT1 A 0 A_JumpIfInventory("GLReloading",1,2)
		Goto Ready
	Reload:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("GLReloading",1,4)
		TNT1 A 0 A_JumpIfInventory("ARGrenade",1,"Fidget")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto NoAmmo
		AGLR ABCD 1
		AGLR E 1 A_PlaySound("assaultrifle/grenadeopen",CHAN_WEAPON)
		AGLR FGHIJKLMN 1
		AGLR OOO 8
		AGLR PQ 1
		AGLR RST 2
		AGLR U 2 A_PlaySound("assaultrifle/grenadeload",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,3)
		TNT1 A 0 A_TakeInventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("ARGrenade",1)
		AGLR VW 1
		AGLR X 2 A_PlaySound("assaultrifle/grenadeclose",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("GLReloading",1)
		AGLR DCBA 1
		Goto Ready
	MineThrowFlash:
		GLRS LMNO 1
		TNT1 A 18
		GLRS ABCD 1
		Stop
	KickingFlash:
		GLRG BCDE 1
		GLRG F 8
		GLRG EDCB 1
		Stop
	AirKickingFlash:
		GLRG BCDE 1
		GLRG F 10
		GLRG EDCB 1
		Stop
	SlideKickingStart:
		GLRG BCDE 1
		GLRG F 18
	SlideKickingEnd:
		GLRG F 6
		GLRG EDCB 1
		Stop
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("ARGrenade",1,1)
		Goto Reload
		TNT1 A 0 A_Takeinventory("ARGrenade",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("RocketAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("GLReloading",1)
		Goto ResumeFire
	}
}