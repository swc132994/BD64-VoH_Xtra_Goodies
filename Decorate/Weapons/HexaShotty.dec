Actor HexaShotgunSpawner : CustomInventory
{
scale 0.9
Tag "Hexa Shotgun"
inventory.pickupmessage "You got the Hexa Shotgun!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		QSGP A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		QSGP A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		QSGP A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64HexaShotgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64HexaShotgunTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64HexaShotgun")
		TNT1 A 0 A_SpawnItemEx("64HexaShotgunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

actor 64HexaShotgun : DoomWeapon
{
Scale 0.9
Weapon.SlotNumber 3
Weapon.AmmoType "Shell"
Weapon.AmmoUse 6
Weapon.AmmoGive 6
Tag "Hexa Shotgun"
obituary "%o was obliterated by %k's Hexa Shotgun!"
inventory.pickupmessage "You got the Hexa Shotgun!"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
	states
	{
	Ready:
		HXSG A 1 A_WeaponReady
		loop
	Deselect:
		HXSG A 1 A_Lower
		loop
	Select:
		HXSG A 1 A_Raise
		loop
	Fire:
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(15,16.1,50,13,"ShotPuff")
		TNT1 A 0 A_PlaySound("Weapon/HexaFire",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HXSF AB 2 Bright A_Quake(2,10,0,128)
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF C 3
		HXSF DEFGHIJ 1
		HXSG ABCDEFGHJKL 1
		HXSG M 5
		HXSG NOPQRSTUVWXYZ 1
		HX1G A 1
		TNT1 A 0 A_PlaySound("QSGOPEN",CHAN_AUTO)
		TNT1 AAAAAA 0 A_FireCustomMissile("HexaShotCaseSpawn",0,0,-5,-15)
		HX1G BCD 1
		HX1G E 3
		HX1G FGHIJKLM 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		HX1G NOPQRS 1
		HX1G T 3
		HX1G UVWXYZ 1
		HX2G A 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		HX2G BCDEFGHI 1
		HX2G J 3
		HX2G KLMNOPQRSTUV 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		HX2G WXYZ 1
		HX3G ABCDEFGHIJ 1
		TNT1 A 0 A_PlaySound("QSGCLSE",CHAN_AUTO)
		HX3G KLMNOPQRS 1
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		HXSP A -1
		stop
	}
}

actor 64HexaShotgunTACTICAL : Brutal64Weapon
{
Scale 0.9
Weapon.SlotNumber 3
Weapon.AmmoType "Shell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 4
Weapon.AmmoType2 "HSGAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Tag "Hexa Shotgun"
obituary "%o was obliterated by %k's Hexa Shotgun!"
inventory.pickupmessage "You got the Hexa Shotgun!"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.AMMO_CHECKBOTH
	states
	{
	MineThrowFlash:
		HXSS EFGH 1
		TNT1 A 18
		HXSS ABCD 1
		Stop
	KickingFlash:
		HXSK ABCDEF 1
		HXSK G 4
		HXSK FEDCBA 1
		Stop
	AirKickingFlash:
		HXSK ABCDEF 1
		HXSK G 6
		HXSK FEDCBA 1
		Stop
	SlideKickingStart:
		HXSK ABCDEF 1
		HXSK G 10
	SlideKickingEnd:
		HXSK G 5
		HXSK FEDCBA 1
		Stop
	v1Ready:
	Ready:
		HXSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Jumpifinventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		HXSS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_JumpIfInventory("HSGReloading",1,"RaiseReloading")
		TNT1 A 0 A_PlaySound("QSGCLSE",CHAN_AUTO)
		HX3G KLMNOPQRS 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	RaiseReloading:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",2,1)
		Goto RaiseNoAmmo
		HXSS ABCD 1 Offset(0,32)
		Goto Reload
	RaiseNoAmmo:
		HXSS ABCD 1 Offset(0,32)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("HSGAlt1",1,"QuadFire")
		TNT1 A 0 A_JumpIfInventory("HSGAlt2",1,"AltFire3")
		Goto AmmoCheck
	ResumeFire:
		TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets (15,16.1,50,13,"ShotPuff",0)
		TNT1 A 0 A_GiveInventory("HSGReloading",1)
		TNT1 A 0 A_PlaySound("Weapon/HexaFire",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HXSF AB 2 Bright A_Quake(4,20,0,256)
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF C 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		HXSF DEF 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		HXSF FG 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		HXSF HI 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		HXSF J 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		HXSG A 1 A_WeaponReady
		Goto Reload
	QuadFire:
		Goto QuadAmmoCheck
	ResumeQuadFire:
		TNT1 AAAAAAAAAAAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets (11.2,9.1,25,7,"ShotPuff",0)
		TNT1 A 0 A_TakeInventory("HSGAlt1",1)
		TNT1 A 0 A_GiveInventory("HSGReloading",1)
		TNT1 A 0 A_PlaySound("QSGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HX3F AB 2 Bright A_Quake(2,10,0,128)
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF C 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		HXSF DEF 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		HXSF FG 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		HXSF HI 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		HXSF J 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		HXSG A 1 A_WeaponReady
		Goto Reload
	AltFire:
		TNT1 A 0 A_JumpIfInventory("HSGAlt1",1,"AltFire2")
		Goto AltAmmoCheck
	ResumeAltFire:
		TNT1 AAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,14,7,"ShotPuff",0)
		TNT1 A 0 A_GiveInventory("HSGAlt1",1)
		TNT1 A 0 A_PlaySound("SSGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HX2F AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF G 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		HXSF HI 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		HXSF J 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		Goto Ready
	AltFire2:
		Goto AltAmmoCheck
	ResumeAltFire2:
		TNT1 AAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,14,7,"ShotPuff",0)
		TNT1 A 0 A_TakeInventory("HSGAlt1",1)
		TNT1 A 0 A_GiveInventory("HSGAlt2",1)
		TNT1 A 0 A_PlaySound("SSGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HX3F AB 2 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF G 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		HXSF HI 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		HXSF J 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		Goto Ready
	AltFire3:
		Goto AltAmmoCheck
	ResumeAltFire3:
		TNT1 AAA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 AA 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAA 0 A_FireCustomMissile("Tracer",random(-6,6),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(8.0,6.0,14,7,"ShotPuff",0)
		TNT1 A 0 A_TakeInventory("HSGAlt2",1)
		TNT1 A 0 A_GiveInventory("HSGReloading",1)
		TNT1 A 0 A_PlaySound("SSGFIRE",CHAN_WEAPON)
		TNT1 A 0 A_ZoomFactor(0.75)
		HXSG A 1 Bright Offset(0,33)
		HXSG A 1 Bright Offset(0,34)
		TNT1 A 0 A_ZoomFactor(1.0)
		HXSF G 3 A_SetPitch(pitch-4,SPF_INTERPOLATE)
		HXSF HI 1 A_SetPitch(pitch+2.6,SPF_INTERPOLATE)
		HXSF J 1 A_SetPitch(pitch+1.2,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		HXSG A 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		HXSG A 1 A_WeaponReady
		Goto Reload
	Reload:
		TNT1 A 0 A_JumpIfInventory("HSGReloading",1,"Reload6")
		TNT1 A 0 A_Jumpifinventory("HSGAmmo",6,"Fidget")
		TNT1 A 0 A_JumpIfInventory("HSGAlt1",1,"StartReload2")
		TNT1 A 0 A_JumpIfInventory("HSGAlt2",1,"StartReload4")
		Goto Reload6
	Reload6:
		TNT1 A 0 A_Jumpifinventory("Shell",2,1)
		Goto NoAmmo
		TNT1 A 0 A_Jumpifinventory("Shell",4,1)
		Goto Reload4
		HXSG ABCDEFGHJKL 1
		HXSG M 5
		HXSG NOPQRSTUVWXYZ 1
		HX1G A 1
		TNT1 A 0 A_PlaySound("QSGOPEN",CHAN_AUTO)
		TNT1 AAAAAA 0 A_FireCustomMissile("HexaShotCaseSpawn",0,0,-5,-15)
		HX1G BCD 1
		HX1G E 3
		HX1G FGHIJKLM 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,3)
		TNT1 A 0 A_GiveInventory("HSGAmmo",2)
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		HX1G NOPQRS 1
		Goto Reload4
	StartReload4:
		HXSG ABCDEFGHJKL 1
		HXSG M 5
		HXSG NOPQRSTUVWXY 1
		HX3R AB 1
		TNT1 A 0 A_PlaySound("QSGOPEN",CHAN_AUTO)
		TNT1 AAAA 0 A_FireCustomMissile("HexaShotCaseSpawn",0,0,-5,-15)
		HX3R CDE 1
	Reload4:
		HX1G T 3
		HX1G UVWXYZ 1
		HX2G A 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,4)
		TNT1 A 0 A_TakeInventory("HSGAlt2",1)
		TNT1 A 0 A_GiveInventory("HSGAmmo",2)
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		HX2G BCDEFGHI 1
		Goto Reload2
	StartReload2:
		HXSG ABCDEFGHJKL 1
		HXSG M 5
		HXSG NOPQRSTUVWXY 1
		HX2R AB 1
		TNT1 A 0 A_PlaySound("QSGOPEN",CHAN_AUTO)
		TNT1 AA 0 A_FireCustomMissile("HexaShotCaseSpawn",0,0,-5,-15)
		HX2R CDE 1
	Reload2:
		HX2G J 3
		HX2G KLMNOPQRSTUV 1
		TNT1 A 0 A_PlaySound("QSGIN",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,4)
		TNT1 A 0 A_TakeInventory("HSGAlt1",1)
		TNT1 A 0 A_GiveInventory("HSGAmmo",2)
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		HX2G WXYZ 1
	FinishReload:
		TNT1 A 0 A_TakeInventory("HSGReloading",1)
		HX3G ABCDEFGHIJ 1
		TNT1 A 0 A_PlaySound("QSGCLSE",CHAN_AUTO)
		HX3G KLMNOPQRS 1
		TNT1 A 0 A_Refire
		Goto Ready
	NoAmmo:
		QSGI A 10
		TNT1 A 0 A_PlaySound("DRYFIRE",CHAN_WEAPON)
		Goto Ready
	Fidget:
		HX3G rQPONMLK 1 A_WeaponReady
		HX3G JIH 1 Offset(-18,55) A_WeaponReady
		TNT1 A 0 A_PlaySound("QSGOPEN",CHAN_AUTO)
		HX3G G 15 A_WeaponReady
		HX3G HIJ 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("QSGCLSE",CHAN_AUTO)
		HX3G KLMNOPQRS 1
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_Jumpifinventory("HSGAmmo",6,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("HSGAmmo",6,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_Jumpifinventory("Shell",6,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("Shell",6,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	QuadAmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"QuadNoReload")
		TNT1 A 0 A_Jumpifinventory("HSGAmmo",4,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("HSGAmmo",4,TIF_NOTAKEINFINITE)
		Goto ResumeQuadFire
	QuadNoReload:
		TNT1 A 0 A_Jumpifinventory("Shell",4,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("Shell",4,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("HSGReloading",1)
		Goto ResumeQuadFire
	AltAmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		TNT1 A 0 A_Jumpifinventory("HSGAmmo",2,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("HSGAmmo",2,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("HSGAlt1",1,"ResumeAltFire2")
		TNT1 A 0 A_JumpIfInventory("HSGAlt2",1,"ResumeAltFire3")
		Goto ResumeAltFire
	AltNoReload:
		TNT1 A 0 A_Jumpifinventory("Shell",2,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("HSGAlt1",1,"ResumeAltFire2")
		TNT1 A 0 A_JumpIfInventory("HSGAlt2",1,"ResumeAltFire3")
		Goto ResumeAltFire
	Spawn:
		HXSP A -1
		stop
	}
}

ACTOR HexaShotCaseSpawn : 50CaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_CustomMissile("ShotgunCasing",0,0,random(120,160),2,random(100,120))
		Stop
	}
}

Actor HSGAlt1 : Inventory
{
inventory.maxamount 1
}

Actor HSGAlt2 : Inventory
{
inventory.maxamount 1
}

Actor HSGReloading : Inventory
{
inventory.maxamount 1
}

Actor HSGAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 6
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 6
Inventory.Icon "HXGPA0"
+IGNORESKILL
}