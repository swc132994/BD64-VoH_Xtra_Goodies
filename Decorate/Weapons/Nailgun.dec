Actor NailgunSpawner : CustomInventory
{
scale 0.82
Inventory.PickupMessage "You got the NailGun!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		NLMG I 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		NLMG I 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		NLMG I -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Nailgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64NailgunTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Nailgun")
		TNT1 A 0 A_SpawnItemEx("64NailgunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64Nailgun : DoomWeapon
{
Scale 0.82
Weapon.AmmoUse 1
Weapon.AmmoGive 25
Weapon.AmmoType "64NailAmmo"
Weapon.SlotNumber 6
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the NailGun!"
Obituary "%o was nailed by %k."
Tag "Nailgun"
+WEAPON.NOAUTOAIM
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		NLMG I -1
		Stop
	Select:
		NLMG A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		NLMG A 1 A_Lower
		Loop
	Ready:
		NLMG A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("64NailAmmo",1,"FireNail1")
		Goto Ready
	FireNail1:
		TNT1 A 0 A_TakeInventory("64NailAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"FireNail2")
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1,0,0,0)
		NLMG B 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),0,3,0,0,Random(2,-2))
		NLMG CD 1
		NLMG C 1
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG B 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	FireNail2:
		TNT1 A 0 A_TakeInventory("64NailAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1,0,0,0)
		NLMG F 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),0,-3,0,0,Random(2,-2))
		NLMG GH 1
		NLMG G 1
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG F 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	}
}

Actor 64NailgunTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.AmmoUse1 1
Weapon.AmmoGive1 25
Weapon.AmmoType1 "64NailAmmo"
//Weapon.AmmoUse2 1
//Weapon.AmmoType2 "64NailOverheat"
Weapon.SlotNumber 6
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the NailGun!"
Obituary "%o was nailed by %k."
Tag "Nailgun"
+WEAPON.NOAUTOAIM
+INVENTORY.RESTRICTABSOLUTELY
//+WEAPON.AMMO_OPTIONAL
//+WEAPON.ALT_AMMO_OPTIONAL
	States
	{
	Spawn:
		NLMG I -1
		Stop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		NLSL FGHIJ 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("NAILPKUP")
		NLSL ABCDE 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		NLMG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("64NailAmmo",1,"FireNail1")
		Goto Ready
	FireNail1:
		TNT1 A 0 A_TakeInventory("64NailAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"FireNail2")
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1,0,0,0)
		NLMG B 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),0,3,0,0,Random(2,-2))
		NLMG CD 1
		NLMG C 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG B 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
//		TNT1 A 0 A_GiveInventory("64NailOverheat",1)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	FireNail2:
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1,0,0,0)
		NLMG F 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),0,-3,0,0,Random(2,-2))
		NLMG GH 1
		NLMG G 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG F 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
//		TNT1 A 0 A_GiveInventory("64NailOverheat",1)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	Reload:
		NLFI ABCDEFGHJK 1 A_WeaponReady
		TNT1 A 0 A_Jump(64,"AltInspect")
		NLFI LMN 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget1",1)
		NLFI OPQRRRR 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget2",1)
		NLFI STUVXYDCBA 1 A_WeaponReady
		Goto Ready
	AltInspect:
		NLF2 AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget1",1)
		NLF2 CDEFGGGGG 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget2",1)
		NLF2 HIJKLMNOP 1 A_WeaponReady
		NLFI DCBA 1 A_WeaponReady
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("64NailAmmo",1,"AltProceed")
		Goto Ready
	AltProceed:
//		TNT1 A 0 A_JumpIfInventory("64NailOverheat",2,"LavaNail1")
//		TNT1 A 0 A_Print("Not hot enough!")
//		Goto Ready
	LavaNail1:
		TNT1 A 0 A_TakeInventory("64NailAmmo",3,TIF_NOTAKEINFINITE)
//		TNT1 A 0 A_TakeInventory("64NailOverheat",2)
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		NLMG A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"LavaNail2")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1,0,0,0)
		NLLA B 2 Bright A_FireCustomMissile("64PlayerLavaNail",Random(8,-8),0,3,0,0,Random(4,-4))
		NLLA CD 1 Bright
		NLMG C 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG B 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	LavaNail2:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1,0,0,0)
		NLLA F 2 Bright A_FireCustomMissile("64PlayerLavaNail",Random(8,-8),0,-3,0,0,Random(4,-4))
		NLLA GH 1 Bright
		NLMG G 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG F 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	MineThrowFlash:
		NLSL FGHIJ 1
		TNT1 A 16
		NLSL ABCDE 1
		Stop
	KickingFlash:
		NLSL EDCB 1
		NLSL A 8
		NLSL BCDE 1
		Stop
	AirKickingFlash:
		NLSL EDCB 1
		NLSL A 10
		NLSL BCDE 1
		Stop
	SlideKickingStart:
		NLSL EDCB 1
		NLSL A 17
	SlideKickingEnd:
		NLSL A 6
		NLSL BCDE 1
		Stop
	}
}

Actor 64NailAmmo : Ammo
{
Scale 0.7
Radius 16
Height 8
Inventory.PickupMessage "Picked up some nails."
Inventory.Icon "NBOXA0"
Inventory.PickupSound "IPICK"
Inventory.Amount 15
Inventory.MaxAmount 300
Ammo.BackpackAmount 15
Ammo.BackpackMaxAmount 600
	States
	{
	Spawn:
		NBOX A -1
		Stop
	}
}

ACTOR 64NailCrate : 64NailAmmo
{
Scale 1.0
Inventory.PickupMessage "Picked up a crate of nails."
Inventory.Amount 40
	States
	{
	Spawn:
		NBOX B -1
		Stop
	}
}

/*Actor 64NailOverheat : Ammo
{
Radius 16
Height 8
Inventory.PickupMessage "Nailgun Overheating!"
Inventory.Icon "TNT1A0"
Inventory.Amount 1
Inventory.MaxAmount 80
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}*/


Actor 64PlayerNail
{
Scale 0.5
Radius 3
Height 3
Speed 44
Damage 4
SeeSound ""
DeathSound ""
Decal BulletChip
DamageType CutLess
Projectile
+BloodSplatter
	States
	{
	Spawn:
		DART A 1 NoDelay A_SpawnItemEx("64NailBlur",0,0,0,1,0,0,180,128)
		TNT1 A 0 A_PlaySound("Weapons/NailFlight")
		Loop
	Crash:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/NailHit")
		TNT1 A 1 A_SpawnItemEx("RicoChet")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("Weapons/NailHitBleed")
		TNT1 A 1 A_SpawnItemEx("RicoChet")
		Stop
	}
}

Actor 64PlayerLavaNail : 64PlayerNail
{
Damage 8
SeeSound "Weapons/NailMGLava"
DamageType Plasma
+FORCEPAIN
	States
	{
	Spawn:
		L4RT A 1 Bright NoDelay A_SpawnItemEx("RedFlareSmall",0,0,0,1,0,0,180,128)
		TNT1 A 0 A_PlaySound("Weapons/NailFlight")
		Loop
	}
}

Actor 64NailBlur
{
Scale 0.6
Height 8
Radius 1
Damage 0
Speed 0.2
RenderStyle Translucent
Alpha 0.5
+NoGravity
+DropOff
+NoTeleport
	States
	{
	Spawn:
		DART A 1 A_FadeOut(0.4)
		Wait
	}
}

Actor NailgunFire2ndCannon : Inventory
{
inventory.maxamount 1
}