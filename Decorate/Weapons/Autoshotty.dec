Actor AssaultShotgunSpawner : CustomInventory	// Replaces Shotgun		//replaced by new spawner
{
Scale 0.87
Inventory.PickupMessage "You got the Assault Shotgun!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		ASGP A 0 NoDelay A_JumpIf(CallACS("D64WeaponsOnlyReplacer")==1,"SpawnSSG")	//D64 weapon only
		SK12 F 0 A_JumpIf(CallACS("Striker12ACS")==true,2)	//Alternative Skin
		ASGP A 0
		"####" "#" 2
		"####" "#" 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		"####" "#" 2
		"####" "#" 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_JumpIf(CallACS("Striker12ACS")==true,2)
		TNT1 A 0 A_GiveInventory("64AssaultShotgun",1)
		Stop
		TNT1 A 0 A_GiveInventory("64Protecta",1)
		Stop
	Pickup2:
		TNT1 A 0 A_JumpIf(CallACS("Striker12ACS")==true,2)
		TNT1 A 0 A_GiveInventory("64AssaultShotgunTactical",1)
		Stop
		TNT1 A 0 A_GiveInventory("64ProtectaTactical",1)
		Stop
	SpawnSSG:
		TNT1 A 1 A_SpawnItemEx("SuperShotgunSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERTID,0,tid)
		Stop
	DMSpawn:
		TNT1 A 0 A_JumpIf(CallACS("Striker12ACS")==TRUE,"DMSpawn2")
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64AssaultShotgun")
		TNT1 A 0 A_SpawnItemEx("64AssaultShotgunTACTICAL")
		TNT1 A 1050
		Loop
	DMSpawn2:
		TNT1 A 0 A_JumpIf(CallACS("Striker12ACS")==FALSE,"DMSpawn")
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Protecta")
		TNT1 A 0 A_SpawnItemEx("64ProtectaTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor AssaultShotgunCounter : Inventory
{
inventory.maxamount 3
}

Actor 64AssaultShotgun : DoomWeapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SelectionOrder 1300
Weapon.AmmoType "64BlueShell"
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Obituary "%o got shot into pieces by %k's Assault Shotgun"
Tag "Assault Shotgun"
VisibleToPlayerClass "64DoomPlayer"
Inventory.PickupMessage "You got the Assault Shotgun!"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	ProtectaSelected:
		TNT1 A 0 A_GiveInventory("SisterWeaponSelector",1)
	Ready:
		ASGG A 1 A_WeaponReady
		Wait
	Select:
		TNT1 A 0 A_JumpIf(CallACS("Striker12ACS")==true,"ProtectaSelected")
		ASGG A 1 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGG A 1 A_Lower
		Wait
	Fire:
		ASGG A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		ASGF A 1
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLFireSounds")
		TNT1 A 0 A_ZoomFactor(0.95)
		ASGF BC 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGF C 3 Bright
		ASGF DE 1
		ASGG A 2
		TNTA A 0 A_Giveinventory("AssaultShotgunCounter")
		ASGA A 0 A_JumpIfInventory("AssaultShotgunCounter",3,"Pump")
		TNT1 A 0 A_Refire
		Goto Ready
	Pump:
		TNT1 A 0 A_TakeInventory("AssaultShotgunCounter",3)
		ASGG BCDEF 1
		TNT1 AAA 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLPumpSounds")
		ASGG GHIJKLMNNNMLKJIHGFEDCB 1
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		ASGP A -1
		Stop
	}
}

Actor 64Protecta : DoomWeapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SelectionOrder 1300
Weapon.AmmoType "64BlueShell"
Weapon.AmmoUse 1
Weapon.AmmoGive 12
Obituary "%o got shot into pieces by %k's Striker-12"
Tag "Striker-12"
VisibleToPlayerClass "64DoomPlayer"
Inventory.PickupMessage "You got the Striker-12!"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Ready:
		SK12 A 1 A_WeaponReady
		Wait
	Select:
		SK12 A 1 A_Raise
		Wait
	Deselect:
		SK12 A 1 A_Lower
		Wait
	Fire:
		SK12 A 1
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(4.0,4.0,8,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("Striker-12/Fire",1)
		TNT1 A 0 A_ZoomFactor(0.95)
		SK13 AB 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		SK13 C 5
		SK13 DE 1
		SK12 A 2
		TNT1 A 0 A_Refire
		Goto Ready
	Spawn:
		SK12 F -1
		Stop
	}
}

Actor AssaultShotgunAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 10
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 10
Inventory.Icon "shota0"
+IGNORESKILL
}

Actor 64AssaultShotgunTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SelectionOrder 1301
Weapon.AmmoType1 "64BlueShell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 10
Weapon.AmmoType2 "AssaultShotgunAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 10
Inventory.PickupMessage "You got the Assault Shotgun!"
Obituary "%o got shot into pieces by %k's Assault Shotgun"
Tag "Assault Shotgun"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.AMMO_CHECKBOTH
+NOALERT
	States
	{
	MineThrowFlash:
		ASGS DCBA 1
		TNT1 A 18
		ASGS ABCD 1
		Stop
	KickingFlash:	//new kick animations
		ASGG BCDEF 1
		ASGG G 6
		ASGG FEDCB 1
		Stop
	ReloadingKick:
		ASGG F 16
		Stop
	AirKickingFlash:
		ASGG BCDEF 1
		ASGG G 8
		ASGG FEDCB 1
		Stop
	SlideKickingStart:
		ASGG BCDEF 1
		ASGG G 10
	SlideKickingEnd:
		ASGG G 5
		ASGG FEDCB 1
		Stop
	Spawn:
		ASGP A -1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
	OkToFire:
		ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto Ready
	NoAmmo:
		ASGG A 10
		TNT1 A 0 A_PlaySound("DRYFIRE")
		Goto Ready
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLPumpSounds")
		ASGS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		ASGS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Fidget:
		ASGG BCDEFG 1 A_WeaponReady
		ASGG H 1 ACS_NamedExecuteAlways("ASGHLPumpSounds")
		ASGG IJKLMMLKJIHGFEDCB 1 A_WeaponReady
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("AssaultShotgunAmmo",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("64BlueShell",1,TIF_NOTAKEINFINITE)
		Goto ResumeFire
	ResumeFire:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLFireSounds")
		TNT1 A 0 A_AlertMonsters
		ASGF A 1
		TNT1 A 0 A_ZoomFactor(0.95)
		ASGF BC 1 Bright
		TNT1 A 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGF C 2 Bright A_SetPitch(pitch-2)
		ASGF D 1 A_SetPitch(pitch+0.8)
		ASGF E 1 A_SetPitch(pitch+0.6)
		ASGG A 1 A_SetPitch(pitch+0.4)
		ASGG A 1 A_SetPitch(pitch+0.2)
		TNT1 A 0 A_ReFire
		Goto OkToFire
	AltFire:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,1)
		Goto Pump
		TNT1 A 0 A_TakeInventory("AssaultShotgunAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",2,"ResumeAltFire3")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",1,"ResumeAltFire2")
		Goto ResumeAltFire1
	AltNoReload:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("64BlueShell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",2,"ResumeAltFire3")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",1,"ResumeAltFire2")
		Goto ResumeAltFire1
	ResumeAltFire1:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 A_GiveInventory("AssaultShotgunCounter",1)
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLFireSounds")
		TNT1 A 0 A_AlertMonsters
		ASGF A 1 Bright
		TNT1 A 0 A_ZoomFactor(0.95)
		ASGF BC 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto AltFire
	ResumeAltFire2:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 A_GiveInventory("AssaultShotgunCounter",1)
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLFireSounds")
		TNT1 A 0 A_AlertMonsters
		ASGF A 1 Bright
		TNT1 A 0 A_ZoomFactor(0.95)
		ASGF BC 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto AltFire
	ResumeAltFire3:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 A_GiveInventory("AssaultShotgunCounter",1)
		TNT1 A 0 ACS_NamedExecuteAlways("ASGHLFireSounds")
		TNT1 A 0 A_AlertMonsters
		ASGF A 1 Bright
		TNT1 A 0 A_ZoomFactor(0.95)
		ASGF BC 1 Bright
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGF C 2 Bright A_SetPitch(pitch-2)
		ASGF D 1 A_SetPitch(pitch+0.8)
		ASGF E 1 A_SetPitch(pitch+0.6)
		ASGG A 1 A_SetPitch(pitch+0.4)
		ASGG A 1 A_SetPitch(pitch+0.2)
		Goto Pump
	Pump:
		ASGG BCDEFG 1 A_WeaponReady(WRF_NOPRIMARY)
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",3,"Eject3")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",2,"Eject2")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",1,"Eject1")
		Goto Reload
	Eject3:
		TNT1 AAA 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_Takeinventory("AssaultShotgunCounter",3)
		Goto FinishPump
	Eject2:
		TNT1 AA 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_Takeinventory("AssaultShotgunCounter",2)
		Goto FinishPump
	Eject1:
		TNT1 A 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-4,-4)
		TNT1 A 0 A_Takeinventory("AssaultShotgunCounter",1)
		Goto FinishPump
	FinishPump:
		ASGG H 1 ACS_NamedExecuteAlways("ASGHLPumpSounds")
		ASGG IJ 1
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,1)
		Goto InsertingShells
		ASGG KLMNNNMLKJ 1
		ASGG IHGFEDCB 1 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,1)
		Goto ReadyToFireAgain
	ReadyToFireAgain:
		ASGG A 1 Offset(10,36)	// <-- New frames
		ASGG A 1 Offset(9,40)	//
		ASGG A 1 Offset(4,40)	//
		ASGG A 1 Offset(0,32)	//
		TNT1 A 0 A_Refire
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",10,"Fidget")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,"ReloadNormally")
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto Ready
		ASGG BCDEF 1
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",9,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty
		TNT1 A 0 A_GiveInventory("AssaultShotgunAmmo",1)
		TNT1 A 0 A_TakeInventory("64BlueShell",1,TIF_NOTAKEINFINITE)
	InsertShellAnimation:
		ASGG F 3
		ASGR ABCDEF 1
		ASGR F 1 A_PlaySound("inshell")
		ASGR GHIJKLM 1 A_WeaponReady(WRF_NOBOB|WRF_NOPRIMARY)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickReloading")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",10,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunAmmo",1,1)
		Goto InsertingShells
		ASGR M 1 A_WeaponReady(WRF_NOBOB)
		Goto InsertingShells
	CheckIfFinishReload:
		ASGG F 1
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto PumpNewShell
		TNT1 A 0 A_Giveinventory("AssaultShotgunAmmo",1)
		TNT1 A 0 A_Takeinventory("64BlueShell",1,TIF_NOTAKEINFINITE)
		Goto InsertShellAnimation
	FinishInsertingShells:
		ASGG F 1
		ASGG FEDCBA 1 A_WeaponReady
		Goto Ready
	PumpNewShell:
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		ASGG F 1
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",3,"Eject3")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",2,"Eject2")
		TNT1 A 0 A_JumpIfInventory("AssaultShotgunCounter",1,"Eject1")
		Goto FinishPump
	DoKickReloading:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheckReloading")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("Stamina",80,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",20)
	ModernCheckReloading:
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickReloadingChecker1")
		Goto InitializeReloadingKick
	LowKickReloadingChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeReloadingKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeReloadingKick:
		TNT1 A 0 A_GunFlash("ReloadingKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto InsertingShells
	Tired:
		TNT1 A 0 A_Print("Not Enough Stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		ASGG F 15
		Goto Ready
	}
}

Actor ProtectaAmmo : Ammo
{
Inventory.Amount 0
Inventory.MaxAmount 12
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 12
Inventory.Icon "shota0"
+IGNORESKILL
}

Actor 64ProtectaTACTICAL : Brutal64Weapon
{
Scale 0.87
Weapon.SlotNumber 3
Weapon.SelectionOrder 1301
Weapon.AmmoType1 "64BlueShell"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 12
Weapon.AmmoType2 "ProtectaAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 12
Inventory.PickupMessage "You got the Striker-12!"
Obituary "$OB_MPSHOTGUN"
Tag "Striker-12"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+WEAPON.AMMO_CHECKBOTH
+NOALERT
	States
	{
	MineThrowFlash:
		SK15 DCBA 1
		TNT1 A 18
		SK15 ABCD 1
		Stop
	KickingFlash:	//new kick animations
		SK12 BCD 1
		SK12 E 10
		SK12 DCB 1
		Stop
	ReloadingKick:
		SK14 I 16
		Stop
	AirKickingFlash:
		SK12 BCD 1
		SK12 E 12
		SK12 DCB 1
		Stop
	SlideKickingStart:
		SK12 BCD 1
		SK12 E 12
	SlideKickingEnd:
		SK12 E 8
		SK12 DCB 1
		Stop
	Spawn:
		SK12 F -1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		SK12 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Ready2:
		SK16 D 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Loop
	NoAmmo:
		SK12 A 10
		TNT1 A 0 A_PlaySound("DRYFIRE")
		Goto Ready
	Select:
		TNT1 A 0 A_PlaySound("Striker-12/Select",3)
		SK15 ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		SK15 DCBA 1
		TNT1 A 0 A_Lower
		Wait
	Fidget:
		SK14 A 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Striker-12/StartReload",CHAN_WEAPON)
		SK17 BCDEFGIJ 1 A_WeaponReady
		SK14 I 3 A_WeaponReady
		TNT1 A 0 A_PlaySound("Striker-12/Fidget",CHAN_WEAPON)
		SK17 KLMMMMMLK 2 A_WeaponReady
		SK14 I 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Striker-12/FinishReload",CHAN_WEAPON)
		SK17 JIHGFEDCB 1 A_WeaponReady
		SK14 A 1 A_WeaponReady
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		SK12 A 1
		Goto AmmoCheck
	ResumeFire:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,-1,-12,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 A_PlaySound("Striker-12/Fire",1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.95)
		SK13 AB 1 Bright
		TNT1 A 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-8,-8)
		TNT1 A 0 A_ZoomFactor(1.0)
		SK13 C 6 A_SetPitch(pitch-2)
		SK13 D 1 A_SetPitch(pitch+0.8)
		SK13 E 1 A_SetPitch(pitch+0.6)
		SK12 A 1 A_SetPitch(pitch+0.4)
		SK12 A 1 A_SetPitch(pitch+0.2)
		TNT1 A 0 A_ReFire
		Goto Ready
	ZoomedFire:
		Goto AmmoCheck
	ResumeZoomedFire:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,40)
		TNT1 AAAAA 0 A_FireCustomMissile("Tracer",random(-5,5),0,0,0,0,random(-5,5))
		TNT1 A 0 A_FireBullets(2.0,2.0,8,7,"ShotgunPuff",0)
		TNT1 A 0 A_PlaySound("Striker-12/Fire",1)
		TNT1 A 0 A_AlertMonsters
		SK16 E 2 Bright
		TNT1 A 0 A_FireCustomMissile("BlueShotCaseSpawn",0,0,-6,-8)
		SK16 F 6 A_SetPitch(pitch-2)
		SK16 G 1 A_SetPitch(pitch+0.8)
		SK16 H 1 A_SetPitch(pitch+0.6)
		SK16 D 1 A_SetPitch(pitch+0.4)
		SK16 D 1 A_SetPitch(pitch+0.2)
		TNT1 A 0 A_ReFire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.2)
		SK16 ABC 1
		Goto Ready2
	NoAim:
		SK16 CBA 1
		TNT1 A 0 A_Takeinventory("Zoomed",0)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		SK12 A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",12,"Fidget")
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",1,"ReloadNormally")
	ReloadNormally:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto Ready
		TNT1 A 0 A_PlaySound("Striker-12/StartReload",CHAN_WEAPON)
		SK14 ABCDEFGH 1
	InsertingShells:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto FinishInsertingShells
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",12,"CheckIfFinishReload")	//reloads 8 if shotgun completly empty
		TNT1 A 0 A_GiveInventory("ProtectaAmmo",1)
		TNT1 A 0 A_TakeInventory("64BlueShell",1,TIF_NOTAKEINFINITE)
	InsertShellAnimation:
		SK14 I 3
		SK14 JKLMNO 1
		SK14 P 1 A_PlaySound("Striker-12/InShell",CHAN_WEAPON)
		SK14 QRST 1 A_WeaponReady(WRF_NOBOB|WRF_NOPRIMARY)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickReloading")	//delay for autofiring
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",12,"FinishInsertingShells")
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",1,1)
		Goto InsertingShells
		SK14 I 1 A_WeaponReady(WRF_NOBOB)
		Goto InsertingShells
	CheckIfFinishReload:
		SK14 I 1
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto FinishInsertingShells
		TNT1 A 0 A_Giveinventory("ProtectaAmmo",1)
		TNT1 A 0 A_Takeinventory("64BlueShell",1,TIF_NOTAKEINFINITE)
		Goto InsertShellAnimation
	FinishInsertingShells:
		TNT1 A 0 A_PlaySound("Striker-12/FinishReload",CHAN_WEAPON)
		SK14 IHGFEDCBA 1 A_WeaponReady
		Goto Ready
	DoKickReloading:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Kicking",1)
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"ModernCheckReloading")
		TNT1 A 0 A_JumpIfInventory("istacticalclass",1,1)
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("Stamina",80,"Tired")
		TNT1 A 0 A_GiveInventory("Stamina",20)
	ModernCheckReloading:
		TNT1 A 0 A_JumpIf(pitch > 32,"LowKickReloadingChecker1")
		Goto InitializeReloadingKick
	LowKickReloadingChecker1:
		TNT1 A 0 A_JumpIf(pitch > 90,"InitializeReloadingKick")
		TNT1 A 0 A_FireCustomMissile("KickAttackDetectDowned",0,0,0,0)
	InitializeReloadingKick:
		TNT1 A 0 A_GunFlash("ReloadingKick")
		TNT1 A 0 A_PlaySound("KICK")
		TNT1 A 0 SetPlayerProperty(0,1,0)
		KICK BCD 1
		TNT1 A 0 A_FireCustomMissile("KickAttack",0,0,0,-7)
		KICK H 5
		KICK I 1
		KICK GFEDCBA 1
		TNT1 A 0 SetPlayerProperty(0,0,0)
		Goto InsertingShells
	Tired:
		TNT1 A 0 A_Print("Not Enough Stamina!",1)
		TNT1 A 0 A_PlaySound("Tired",2)
		SK14 I 15
		Goto Ready
	AmmoCheck:
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		TNT1 A 0 A_JumpIfInventory("ProtectaAmmo",1,1)
		Goto Reload
		TNT1 A 0 A_TakeInventory("ProtectaAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeZoomedFire")
		Goto ResumeFire
	NoReload:
		TNT1 A 0 A_JumpIfInventory("64BlueShell",1,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("64BlueShell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ResumeZoomedFire")
		Goto ResumeFire
	}
}

ACTOR 10GCasing : ShotgunCasing
{
Scale 0.20
	States
	{
	Spawn:
		CAS6 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(0-ScaleX,ScaleY)	//FLIP IMAGE HORZ
		CAS6 ABCDEFGH 3 A_CheckFloor("Death")
		WAIT
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("LowBloodCheckerACS")//Check Effects
		TNT1 A 0 A_Jump(256,"Rest1","Rest2","Rest3")
		Goto Rest1
	Rest1:
		CAS6 I 400
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode",1,"Stoping")
		Loop
	Rest2:
		CAS6 J 400
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode",1,"Stoping")
		Loop
	Rest3:
		CAS6 K 400
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode",1,"Stoping")
		Loop
	Stoping:
		TNT1 A 1
		Stop
	}
}

ACTOR BlueShotCaseSpawn : 50CaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_CustomMissile("10GCasing",0,0,random(-80,-100),2,random(40,60))
		Stop
	}
}

Actor 64BlueShell : Ammo
{
Scale 0.82
Inventory.PickupSound "IPICK"
Inventory.PickupMessage "Picked up 2 Ultra Shells."
Inventory.Amount 2
Inventory.MaxAmount 25
Ammo.BackpackAmount 2
Ammo.BackpackMaxAmount 50
Inventory.Icon "10GUA0"
	states
	{
	Spawn:
		10GU A -1
		Stop
	}
}

Actor 64BlueShellBox : 64BlueShell
{
Scale 1.0
Inventory.PickupMessage "Picked up a box of Ultra Shells."
Inventory.Amount 10
	states
	{
	Spawn:
		10GU B -1
		Stop
	}
}