Actor NadeLauncherSpawner : CustomInventory
{
scale 0.8
Tag "Grenade Launcher"
Inventory.PickupMessage "You got the grenade launcher!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		GLAU A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		GLAU A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		GLAU A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64NadeLauncher",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64NadeLauncherTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64NadeLauncher")
		TNT1 A 0 A_SpawnItemEx("64NadeLauncherTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64NadeLauncher : DoomWeapon
{
Scale 0.8
Weapon.SlotNumber 5
Weapon.SelectionOrder 2550
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the grenade launcher!"
Obituary "%o caught %k's grenade."
Tag "Grenade Launcher"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		GLAU A -1
		Stop
	Ready:
		GRLG A 1 A_WeaponReady
		Loop
	Deselect:
		GRLG A 1 A_Lower
		Loop
	Select:
		GRLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLF A 1 Bright A_FireCustomMissile("64NadeLauncherGrenades")
		TNT1 A 0 A_Recoil(2)
		GRLF BCDE 1 Bright
		GRLF F 2 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		GRLF GHIJK 2
		GRLG A 8 A_WeaponReady
		GRLG A 1 A_Refire
		Goto Ready
	}
}

Actor PestilenceAmmo : Ammo
{
Scale 0.2
Renderstyle Add
Alpha 1
Inventory.Amount 0
Inventory.MaxAmount 50
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 50
Inventory.Icon LENGA0
+IGNORESKILL
+FLOATBOB
	States
	{
	Spawn:
		LENG A -1
		Stop
	}
}

ACTOR 64PestilenceXtra : 64Pestilence Replaces 64Pestilence
{
DropItem "DemonEssenceSpawner"
DropItem "PestilenceAmmo"
DropItem "PestilenceAmmo"
}

Actor 64NadeLauncherTACTICAL : Brutal64Weapon
{
Scale 0.8
Weapon.SlotNumber 5
Weapon.SelectionOrder 2550
Weapon.AmmoType1 "RocketAmmo"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 2
Weapon.AmmoType2 "PestilenceAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the grenade launcher!"
Obituary "%o caught %k's grenade."
Tag "Grenade Launcher"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		GLAU A -1
		Stop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		GRLS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("Grenadier/Ready",CHAN_AUTO)
		GRLS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		GRLG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLF A 2 Bright A_FireCustomMissile("64NadeLauncherGrenades")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(2)
		GRLF B 2 Bright A_SetPitch(pitch-2,SPF_INTERPOLATE)
		GRLF C 2 Bright A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		GRLF D 2 Bright A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		GRLF E 2 Bright A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		GRLF F 2 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		GRLF GHIJK 3
		GRLG A 10 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PestilenceAmmo",1,2)
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLV A 2 A_FireCustomMissile("64NadeLauncherGasnades",0,0)
		TNT1 A 0 A_TakeInventory("PestilenceAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(2)
		GRLV B 2 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		GRLV C 2 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		GRLV D 2 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		GRLV E 2 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		GRLF F 2 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		GRLF GGGHIJK 3
		GRLG A 10 A_ReFire
		Goto Ready
	MineThrowFlash:
		GRLS EFGH 1
		TNT1 A 18
		GRLS ABCD 1
		Stop
	KickingFlash:
		GRLG BCDE 1
		GRLG F 8
		GRLG EDCB 1
		Stop
	SlideKickingStart:
		GRLG BCDE 1
		GRLG F 18
	SlideKickingEnd:
		GRLG F 4
		GRLG EDCB 1
		Stop
	}
}

Actor 64NadeLauncherGrenades : 64NightcrawlerGrenade
{
Damage 10
Gravity 0.6
Speed 15
Species "Marines"
+THRUSPECIES
BounceSound "Grenadier/Bouncy"
}

Actor 64NadeLauncherGasnades : 64NightcrawlerGrenade
{
Damage 1
Gravity 0.75
Speed 10
Species "Marines"
+THRUSPECIES
DeathSound "Grenadier/Gas"
BounceSound "Grenadier/Bouncy"
States
	{
	Spawn:
		GR3N A 0 NoDelay ThrustThingZ(0,10,0,1)
	Spawn2:
		GR3N AAAA 2 A_CustomMissile("GasnadeSmoke",0,0,0,2)
		Loop
	Death:
		GR3N A 1 A_CustomMissile("PoisonShotCloud1",0,0)
		GR3N A 1 A_CustomMissile("PoisonShotCloud1",0,0,90)
		GR3N A 1 A_CustomMissile("PoisonShotCloud1",0,0,-90)
		GR3N A 1 A_CustomMissile("PoisonShotCloud1",0,0,180)
	NadeOnFloor:
		GR3N A 525
		GR3N A 2 A_FadeOut(0.05)
		Wait
	}
}

Actor GasnadeSmoke : OldschoolRocketSmokeTrail2
{
renderstyle stencil
stencilcolor DarkGreen
}